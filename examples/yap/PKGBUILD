pkgname=yap
pkgver=1.49
pkgrel=1
pkgdesc="Package software with ease"
pkgdesc__alpine="Package software with ease for Alpine"
pkgdesc__arch="Package software with ease for Arch"
pkgdesc__debian="Package software with ease for Debian"
pkgdesc__fedora="Package software with ease for Fedora"
pkgdesc__rocky="Package software with ease for Rocky"
pkgdesc__ubuntu="Package software with ease for Ubuntu"
maintainer="The Yap Packager <packager@yap.org>"
copyright=(
  "2023, The Yap Project <https://github.com/M0Rf30/yap>"
)
arch=('x86_64')
license=('GPL-3.0-only')
options=('!strip')
section=misc
priority=optional
url="https://github.com/M0Rf30/${pkgname}"
makedepends=(
  'upx'
)
makedepends__apt=(
  'upx-ucl'
)
source=(
  "git+${url}"
)
sha256sums=(
  "SKIP"
)

build() {
  cd "${srcdir}/${pkgname}"

  CGO_ENABLED=0 \
    go build \
    -buildvcs=false \
    -ldflags "-w -s" \
    cmd/yap/yap.go

  upx "${pkgname}"
}

package() {
  cd "${srcdir}/${pkgname}"

  install -Dm 755 "${pkgname}" \
    "${pkgdir}/usr/bin/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"

  "./${pkgname}" completion bash > \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "./${pkgname}" completion zsh > \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "./${pkgname}" completion fish > \
    "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"

  install -Dm 644 -t \
    "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.md
  install -Dm 644 -t \
    "${pkgdir}/usr/share/doc/${pkgname}" README.md
}
